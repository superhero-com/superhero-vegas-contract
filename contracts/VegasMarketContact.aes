// ISC License
//
// Copyright (c) 2017, aeternity developers
//
// Permission to use, copy, modify, and/or distribute this software for any
// purpose with or without fee is hereby granted, provided that the above
// copyright notice and this permission notice appear in all copies.
//
// THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES WITH
// REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY
// AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY SPECIAL, DIRECT,
// INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM
// LOSS OF USE, DATA OR PROFITS, WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE
// OR OTHER TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION WITH THE USE OR
// PERFORMANCE OF THIS SOFTWARE.


// THIS IS NOT SECURITY AUDITED
// DO NEVER USE THIS WITHOUT SECURITY AUDIT FIRST



@compiler >= 6

include "String.aes"
include "List.aes"

payable contract VegasMarketContact =


    datatype event =
        AddMarketEvent(address, string)
        | SubmitAnswerEvent(address, string, address, int)
        | ReceiveReward(address, int,int)
        | ContractBalance(int)


    record state = {
        //预测的全部内容，哪个用户预测的id的预测内容(用户, map(预测id, 预测细节))，详情页可以直接使用address+id访问
        markets                     : map(address, map(string, market)),
        //进行中的预测，可以理解为是全部预测中的一个缓存分类，用于在首页展示和个人预测的我发起的页面展示
        markets_start               : map(address, map(string, market)),
        //我参与过的预测激励，我自己参与过那个预测并且是哪个人的
        markets_record              : map(address, list(market_rocord)),
        //用户投票记录，用于检测当前用户是否投票过的判断，哪个账户发布的哪个预测的那个用户投票的第几个结果
        user_markets_record         : map(address, map(string, map(address, int))),
        //用户领奖记录，哪个账户发布的哪个预测的那个用户领取的金额
        user_markets_receive_record : map(address, map(string, map(address, int))),
        //某个预测的结果合集
        oracle_market               : map(string, list(int)),
         //已经预测的次数
        oracle_market_count         : map(string, int),
        //法官投票的记录，用于后期审查
        oracle_market_record        : map(string, map(address, int)),
        //合约归属者
        owner                       : address,
        //法官账户
        aggregator_user             : map(address, string),
        //
        certificate                 : map(string, address),
        certificate_people          : map(address, string),
        //配置文件
        config                      : config}

    record market_rocord = {
        //预言id sha256（地址+时间戳）
        market_id        : string,
        //预测发布者地址
        owner            : address,
        //内容
        content          : string,
        //投注了多少钱
        amount           : int,
        //选择的结果
        put_result       : string,
        //选择的结果
        put_result_index : int,
        //选择发生的时间
        put_time         : int,
        //选择发生的高度
        put_height       : int}

    record config = {
        decimals              : int,
        //触发预测完成的次数
        oracle_trigger_count  : int,
        //预测最低的时间限制
        market_min_height     : int,
        //预测最高的时间限制
        market_max_height     : int,
        //预测最低的金额限制
        market_min_amount     : int,
        //预测最高的金额限制
        market_max_amount     : int,
        //交易记录最多缓存条数
        record_max_count      : int,
        //预测最高的时间限制
        market_max_people     : int,
        //平台分红账户地址
        platform_address      : address,
        //基金会分红账户地址
        foundation_address    : address,
        //平台分红百分比
        platform_percentage   : int,
        //基金会分红百分比
        foundation_percentage : int,
        //创建者分红百分比
        create_percentage     : int,
        //用户最大可同时进行的预测数量
        max_market_put_count  : int}


    record market = {
        //预言id sha256（地址+时间戳）
        market_id      : string,
        //发布者
        owner          : address,
        //内容，用于展示给用户的
        content        : string,
        //获取结果的地址，用于查看的
        source_url     : string,
        //可能产生的结果
        answers        : list(answer),
        //创建的时间
        create_time    : int,
        //结束的时间
        over_height    : int,
        //最大投注人数
        max_people     : int,
        //最小投注数量
        min_amount     : int,
        //当前主题一共投注了多少
        total_amount   : int,
        //当前主题一共被领取了多少
        receive_amount : int,
        //多少人进行了投注
        put_count      : int,
        //多少人领取了奖金
        receive_count  : int,
        //最终产生的结果
        result         : int,
        //当前属于什么进度
        progress       : int,
        //预测的类型
        market_type    : int}



    record answer = {
        //答案的内容
        content  : string,
        //选择答案的次数
        count    : int}

    stateful entrypoint
        init : () => state
        init () =
            let owner                     = Call.caller
            let decimals                  = 1000000000000000000
            { markets                     = {},
              markets_start               = {},
              markets_record              = {},
              user_markets_record         = {},
              user_markets_receive_record = {},
              oracle_market               = {},
              oracle_market_count         = {},
              oracle_market_record        = {},
              owner                       = Call.caller,
              aggregator_user             = {},
              certificate                 = {},
              certificate_people          = {},
              config                      = {
                    decimals              = decimals,
                    oracle_trigger_count  = 3,
                    market_min_height     = 1,
                    market_max_height     = 480 * 30,
                    market_min_amount     = decimals / 100,
                    market_max_amount     = decimals * 1000,
                    record_max_count      = 10,
                    market_max_people     = 10,
                    platform_address      = ak_CNcf2oywqbgmVg3FfKdbHQJfB959wrVwqfzSpdWVKZnep7nj4,
                    foundation_address    = ak_CNcf2oywqbgmVg3FfKdbHQJfB959wrVwqfzSpdWVKZnep7nj4,
                    platform_percentage   = 850,
                    foundation_percentage = 150,
                    create_percentage     = 500,
                    max_market_put_count  = 5}}


    /**
     * 发布一个预测，只有聚合器用户才可以发布 status=(0) 的预测
     * - content: 主题
     * - source_url: 获取结果的地址
     * - min_amount: 最小支付数量
     * - over_time: 结束时间戳，从当前时间开始相加
     * - answers: 答案结果
     */
    stateful entrypoint
        add_market : (string, string, int, int, list(answer)) => market
        add_market (content, source_url, min_amount, over_height, answers) =
            //最低结束时间也要大于配置中的时间，比如大于当前时间1天和小于30天
            require(over_height >= state.config.market_min_height, "Over time more than min required")
            require(over_height =< state.config.market_max_height, "Over time more than max required")
            //最大最小金额限制
            require(min_amount =< state.config.market_max_amount, "Market max amount error")
            require(min_amount >= state.config.market_min_amount, "Market min amount error")
            //内容限制
            require(String.length(content) =< 500, "Market conent length > 500 character")
            require(String.length(source_url) =< 100, "Market source_url length > 100 character")
            require(Map.size(get_markets_start(Call.caller)) =< 15, "Market count length > 15")
             //验证答案的次数不等于默认状态抛出错误
            List.foreach(answers, (item) => require(item.count == 0 && String.length(item.content) < 20 ,"Answer count error or conent length > 20"))

            //生成预测ID
            let market_id = generate_market_id()
            //检测是否有重复的id
            restrict_repeat_market(Call.caller,market_id)

            let market_type = get_market_type()

            //构建主题
            let market = {
                market_id      = market_id,
                owner          = Call.caller,
                content        = content,
                source_url     = source_url,
                answers        = answers,
                create_time    = Chain.timestamp,
                over_height    = Chain.block_height + over_height,
                max_people     = get_max_people(),
                min_amount     = min_amount,
                total_amount   = 0,
                receive_amount = 0,
                put_count      = 0,
                receive_count  = 0,
                result         = -1,
                progress       = 0,
                market_type    = market_type}
            put(state {markets[Call.caller = {}][market_id] = market})
            put(state {markets_start[Call.caller = {}][market_id] = market})

            if(market_type == 1)
                put(state {oracle_market_count[market_id] = 0})
                put(state {oracle_market[market.market_id] = []})

            Chain.event(AddMarketEvent(Call.caller,market_id))
            market



    /**
     * 用户提交问题
     * - address: 发布的用户
     * - string: 预测id
     * - int: 第几个答案
     */
    payable stateful entrypoint
        submit_answer : (address,string,int) => bool
        submit_answer (market_address,market_id,answer_index) =
            //获取预测
            let market = get_market(market_address,market_id)
            //如果预测结束时间小于当前时间，提示错误
            require(Chain.block_height =< market.over_height,"Market time out")
            //如果当前支付数量小于预测规定的金额，提示错误
            require(Call.value == market.min_amount,"Market amount low out")
            //如果当前的进度不是 START状态，提示错误
            require(market.progress == 0,"Market progress is not start")
            //如果当前用户已经参与过提问的，提示错误
            require(!is_user_markets_record(market_address,market_id),"User markets repeat")
            //如果参与人到达最大限制,提示错误
            require(market.max_people > market.put_count,"Market max people many")
            //获取要提交的答案
            let answer = get_market_answer(market_address,market_id,answer_index)
            //构建新的答案对象，并将已投注次数+1操作
            let new_answer  = {
                content     = answer.content,
                count       = answer.count + 1}
            //将旧的答案对象替换为新的
            let new_answers = List.replace_at(answer_index, new_answer, state.markets[market_address][market_id].answers)
            put(state {markets[market_address][market_id].answers = new_answers})
            //更新保存当前投注的总额
            put(state {markets[market_address][market_id].total_amount @n = n + Call.value})
            //更新投注的次数
            put(state {markets[market_address][market_id].put_count @n = n + 1})
            //记录当前预测已经投票
            put(state {user_markets_record[market_address = {}][market_id  = {}][Call.caller] = answer_index})
            //更新进行中的预测为最新状态
            put(state {markets_start[market_address = {}][market_id] = state.markets[market_address][market_id]})

            let market_rocord = {
                market_id        = market_id,
                owner            = market_address,
                content          = market.content,
                amount           = Call.value,
                put_result       = new_answer.content,
                put_result_index = answer_index,
                put_time         = Chain.timestamp,
                put_height       = Chain.block_height}


            let records = get_market_records(Call.caller)


            if(List.length(records) >= state.config.record_max_count)
                let market_records = List.reverse(records)
                let market_records = List.drop(List.length(market_records) - 1, market_records)
                let market_records = List.reverse(market_records)
                let market_records = List.insert_at(0, market_rocord, market_records)
                put(state{ markets_record[Call.caller] = market_records})
            else
                let market_records = List.insert_at(0, market_rocord, records)
                put(state{ markets_record[Call.caller] = market_records})

            Chain.event(SubmitAnswerEvent(market_address, market_id, Call.caller, answer_index))
            true

    /**
     * 收集数据，提供结果
     */
    stateful entrypoint
        provide_answer : (address,string,int) =>bool
        provide_answer (market_address,market_id,market_index) =
            //获取当前的预测
            let market = get_market(market_address,market_id)
            //如果提供的结果大于了答案的总数，提示错误
            require(market_index < List.length(market.answers),"Index out error")
            //如果提供的结果是负数提示错误
            require(market_index >= 0, "Index out error 0")
            //如果如果状态不是需要预言机的结果，提示错误
            require(market.market_type  == 1 ,"Market type error")
             //如果进度 不是 START状态，提示错误
            require(market.progress == 0,"Market progress is not start")
            //如果不是有权限的人提供结果，提示错误
            require(is_aggregator_user(),"Aggregator error")
            //如果没有到达预测的结束时间，提示错误
            require(Chain.block_height > market.over_height, "Market time not out")
            //如果当前用于已经对这个预测提供过结果，提示错误
            require(!is_oracle_market_record(market_id),"Provide count more")
            //插入提供的结果数据
            let provide_data = List.insert_at(0,market_index,state.oracle_market[market_id])
            //保存提供的结果
            put(state{oracle_market[market_id] = provide_data})
            //保存当前用户提供预测的结果记录
            put(state{oracle_market_record[market_id = {}][Call.caller] = market_index})
            //保存预测结果数据次数
            put(state{oracle_market_count[market_id = 0] @ n = n + 1})

            if(get_oracle_market_provide_count(market_id) >= state.config.oracle_trigger_count && is_repeat(state.oracle_market[market_id]))
                update_market_progress_to_over(market_address,market_id)

            true


   /**
     * 更新预测到下一步，更改完后的状态用结束状态，领取奖金，
     * 同时也插入等待中的预测，便于UI展示
     * - address: 发布的用户
     * - string: 预测id
     */
    stateful function
        update_market_progress_to_over : (address,string)=>unit
        update_market_progress_to_over (market_address,market_id) =
            //获取当前预测
            let market = get_market(market_address,market_id)
            //如果没有到达预测的结束时间，提示错误
            require(Chain.block_height > market.over_height, "Market time not out")
            //如果如果状态不是需要预言机的结果，提示错误
            require(market.market_type  == 1, "Market type error")
            //如果进度 不是 START状态，提示错误
            require(market.progress == 0,"Market progress is not start")
            //如果没有到达预测的结束时间，提示错误
            require(Chain.block_height > market.over_height, "Market time not out")
            //如果提供者提供的数据不足以达到设置的标准，提示错误
            require(get_oracle_market_provide_count(market_id) >= state.config.oracle_trigger_count, "Market provide count low")

            //删除进行中的预测，因为已经进入等待状态了
            put(state {markets_start[market_address] = Map.delete(market_id,state.markets_start[market_address])})
            //更新状态到已结束状态
            put(state {markets[market_address][market_id].progress = 2})
            //计算哪个投票最多，然后设置最终结果
            let reward_num = most_of(state.oracle_market[market_id])
            //设置最终领奖结果
            put(state {markets[market_address][market_id].result = reward_num})





    /**
     * 私有预测更新到下一步，更改完后的状态用结束状态，领取奖金，
     * 同时也插入等待中的预测，便于UI展示
     * - address: 发布的用户
     * - string: 预测id
     */
    stateful entrypoint
        private_update_market_progress_to_over : (address,string,int)=>bool
        private_update_market_progress_to_over (market_address,market_id,result_index) =
            //获取当前预测
            let market = get_market(market_address,market_id)
            //如果不是发布者本人调用方法，提示错误
            require(market.owner == Call.caller, "Market time not out")
            //如果提供的结果大于了答案的总数，提示错误
            require(result_index < List.length(market.answers), "Index out error")
            //如果没有到达预测的结束时间，提示错误
            require(Chain.block_height > market.over_height, "Market time not out")
            //如果如果状态不是需要预言机的结果，提示错误
            require(market.market_type  == 0, "Market type error")
            //如果进度 不是 START状态，提示错误
            require(market.progress == 0, "Market progress is not start")

            //删除进行中的预测，因为已经进入等待状态了
            put(state {markets_start[market_address] = Map.delete(market_id,state.markets_start[market_address])})

            //如果提供者提供的数据不足以达到设置的标准，提示错误
            put(state {markets[market_address][market_id].progress = 2})
            //计算哪个投票最多，然后设置最终结果
            put(state {markets[market_address][market_id].result = result_index})



            true



    /**
     * 领取奖金，
     */
    stateful entrypoint
        receive_reward : (address,string)=>int
        receive_reward (market_address,market_id) =
            //获取当前预测
            let market = get_market(market_address, market_id)
            //如果进度 不是 OVER状态，提示错误
            require(market.progress == 2, "The game is not over yet")
            //如果当前用户已经参与过提问的，提示错误
            require(!is_user_markets_receive_record(market_address, market_id), "You can't double claim the bonus")
            //如果当前用户没有参与过提问的，提示错误
            require(is_user_markets_record(market_address ,market_id), "You didn't participate in the prediction")
            //如果当前用户投票和最终结果不对，提示错误
            require(market.result == get_user_markets_record_result(market_address, market_id), "Your vote does not match the final result")
            // //获取正确的答案
            let answer = get_market_answer(market_address, market_id,market.result)
            // //计算应该发放的金额
            let amount = market.total_amount / answer.count


            //设置已经领取的了多少数量
            put(state {markets[market_address][market_id].receive_amount  @ n = n + amount})
            put(state {markets[market_address][market_id].receive_count  @ n = n + 1})


            //分红
            Chain.spend(state.config.platform_address, calculate_amount(amount, state.config.platform_percentage))
            Chain.spend(state.config.foundation_address, calculate_amount(amount, state.config.foundation_percentage))

            let user_amount = calculate_amount(
                amount ,
                (10000 - (state.config.platform_percentage + state.config.foundation_percentage +  state.config.create_percentage )))

            // 记录当前预测已经领过奖
            put(state {user_markets_receive_record[market_address = {}][market_id  = {}][Call.caller] = user_amount})

            if(market.market_type == 1)
                let items : list(address * int)  = Map.to_list(get_oracle_market_record(market_id))
                List.foreach(items, (item) => oracle_spend(item, amount, List.length(items)))
            else
                Chain.spend(market.owner, calculate_amount(amount, state.config.create_percentage))

            Chain.spend(Call.caller, user_amount)
            Chain.event(ReceiveReward(Call.caller, user_amount, market.total_amount))
            user_amount




    stateful function
        oracle_spend : ((address * int), int, int) => unit
        oracle_spend((a, c), amount,length) =
            Chain.spend(a, calculate_amount(amount, state.config.create_percentage / length))





    stateful entrypoint
        add_certificate : (string) => unit
        add_certificate(name) =
            protocol_restrict()
            switch(AENS.lookup(name))
                Some(AENS.Name(owner, _, _)) =>
                    put(state {certificate[name] = owner})
                    put(state {certificate_people[owner] = name})

                None =>
                    abort("Name not find")


    stateful entrypoint
        delete_certificate:(string) => bool
        delete_certificate (name) =
            protocol_restrict()
            put(state {certificate = Map.delete(name,state.certificate)})
            true


    stateful entrypoint
        add_aggregator_user : (address,string) => address
        add_aggregator_user(account,name) =
            protocol_restrict()
            put(state {aggregator_user[account]= name})
            account

    //删除法官用户
    stateful entrypoint
        delete_aggregator_user:(address) => bool
        delete_aggregator_user (addr) =
            protocol_restrict()
            put(state {aggregator_user = Map.delete(addr,state.aggregator_user)})
            true


    //删除法官用户
    stateful entrypoint
        chagne_owner:(address) => address
        chagne_owner (addr) =
            protocol_restrict()
            put(state {owner = addr})
            addr




    /**
     * 获取预测已经有几个人提供了结果
     */
    entrypoint
        get_oracle_market_provide_count : (string) => int
        get_oracle_market_provide_count (market_id) =
            state.oracle_market_count[market_id]





    /**
     * 获取公共具体预测
     * - market_address: 发布者的地址
     * - market_id: 发布者的具体预测
     */
    entrypoint
        get_market_public : (address) => map(string, market)
        get_market_public(market_address) =
            switch(Map.lookup(market_address, state.markets))
                Some(market_map) => market_map
                None => abort("Dnt find the market")

    /**
     * 获取具体预测
     * - market_address: 发布者的地址
     * - market_id: 发布者的具体预测
     */
    entrypoint
        get_market : (address,string) => market
        get_market(market_address,market_id) =
            switch(Map.lookup(market_address, state.markets))
                Some(market_map) =>
                    switch(Map.lookup(market_id, market_map))
                        Some(market) =>  market
                        None => abort("Dnt find the market")
                None => abort("Dnt find the market")


    /**
     * 当前用户是否领取过奖金
     */
    entrypoint
        is_user_markets_receive_record : (address,string) => bool
        is_user_markets_receive_record(market_address,market_id) =
            switch(Map.lookup(market_address, state.user_markets_receive_record))
                Some(market_map) =>
                    switch(Map.lookup(market_id, market_map))
                        Some(account) =>
                            switch(Map.lookup(Call.caller, account))
                                Some(account) =>  true
                                None => false
                        None => false
                None => false

    /**
     * 当前用户是否参与过预测
     */
    entrypoint
        is_user_markets_record : (address,string) => bool
        is_user_markets_record(market_address,market_id) =
            switch(Map.lookup(market_address, state.user_markets_record))
                Some(market_map) =>
                    switch(Map.lookup(market_id, market_map))
                        Some(account) =>
                            switch(Map.lookup(Call.caller, account))
                                Some(account) =>  true
                                None => false
                        None => false
                None => false


    /**
     * 获取当前用于预测投票的结果
     */
    entrypoint
        get_user_markets_record_result : (address,string) => int
        get_user_markets_record_result(market_address,market_id) =
            switch(Map.lookup(market_address, state.user_markets_record))
                Some(market_map) =>
                    switch(Map.lookup(market_id, market_map))
                        Some(account) =>
                            switch(Map.lookup(Call.caller, account))
                                Some(result) =>  result
                                None => -1
                        None => -1
                None => -1

    /**
     * 获取预测下面所有人参与的预测结果
     */
    entrypoint
        get_user_markets_record : (address,string) => map(address,int)
        get_user_markets_record(market_address,market_id) =
            switch(Map.lookup(market_address, state.user_markets_record))
                Some(market_map) =>
                    switch(Map.lookup(market_id, market_map))
                        Some(account) => account
                        None => {}
                None => {}

    /**
     * 获取进行中的预测
     */
    entrypoint
        get_markets_start : (address) => map(string, market)
        get_markets_start(market_address) =
            switch(Map.lookup(market_address, state.markets_start))
                Some(market_map) =>
                    market_map
                None => {}

    /**
     * 获取进行中的预测数量
     */
    entrypoint
        get_markets_start_size : (address) => int
        get_markets_start_size(market_address) =
            switch(Map.lookup(market_address, state.markets_start))
                Some(market_map) =>
                    Map.size(market_map)
                None => 0



    /**
     * 管理员是否投票过
     */
    entrypoint
        is_oracle_market_record : (string) => bool
        is_oracle_market_record(market_id) =
            switch(Map.lookup(market_id, state.oracle_market_record))
                Some(market_record_user) =>
                    switch(Map.lookup(Call.caller, market_record_user))
                        Some(result) =>  true
                        None => false
                None => false

  //获取法官用户
    entrypoint
        get_aggregator_user:() => map(address, string)
        get_aggregator_user () =
            state.aggregator_user




    //获取法官提供的结果
    entrypoint
        get_oracle_market_record:(string) => map(address, int)
        get_oracle_market_record (market_id) =
            switch(Map.lookup(market_id, state.oracle_market_record))
                    Some(market_record_user) => market_record_user
                    None => {}


    /**
     * 获取交易记录数据
     */
    entrypoint
        get_market_records : (address) => list(market_rocord)
        get_market_records(addr) =
            switch(Map.lookup(addr, state.markets_record))
                Some(records) => records
                None => []

    /**
     * 获取交易记录数据大小
     */
    entrypoint
        get_market_records_size : (address) => int
        get_market_records_size(addr) =
            switch(Map.lookup(addr, state.markets_record))
                Some(records) => List.length(records)
                None => 0


    //获取配置信息
    entrypoint
        get_config:() => config
        get_config () =
            state.config

    //获取配置信息
    entrypoint
           get_owner:() => address
           get_owner () =
               state.owner


    entrypoint
        get_state:()=>state
        get_state () =
            state



    stateful function
        get_max_people : () => int
        get_max_people() =
            if(state.owner == Call.caller) 10000000
            else
                List.foreach(Map.to_list(state.certificate), (item) => update_name_address(item))
                switch(Map.lookup(Call.caller, state.certificate_people))
                    Some(v) => 10000000
                    None => state.config.market_max_people

    stateful function
        update_name_address : ((string * address)) => unit
        update_name_address((name, addr)) =
            switch(AENS.lookup(name))
                Some(AENS.Name(owner, _, _)) =>
                    put(state {certificate[name] = owner})
                    put(state {certificate_people[owner] = name})


    function
        calculate_amount : (int, int) => int
        calculate_amount (amount, proportion) =
            amount / 10000 * proportion

    /**
     * 检测是否有重复的预测
     * - market_address: 发布者的地址
     * - market_id: 发布者的具体预测
     */
    function
        restrict_repeat_market : (address,string) => bool
        restrict_repeat_market(market_address,market_id) =
            switch(Map.lookup(market_address, state.markets))
                Some(market_map) =>
                    switch(Map.lookup(market_id, market_map))
                        Some(market) => abort("Market ID Repeat")
                        None => true
                None => true
    /**
     * 当前调用者是否属于可发布聚合器账户列表中
     */
    function
        is_aggregator_user : () => bool
        is_aggregator_user() =
            switch(Map.lookup(Call.caller, state.aggregator_user))
                Some(user) => true
                None => false

    /**
     * 当前调用者是否属于可发布聚合器账户列表中
     */
    function
        get_market_type : () => int
        get_market_type() =
            if(state.owner == Call.caller) 1
            else 0






    /**
     * 获取具体预测的答案
     */
    function
        get_market_answer : (address,string,int) => answer
        get_market_answer(market_address, market_id, answer_index) =
            switch(Map.lookup(market_address, state.markets))
                Some(market_map) =>
                    switch(Map.lookup(market_id, market_map))
                        Some(market) =>  List.get(answer_index, market.answers)
                        None => abort("This option was not found")
                None => abort("This option was not found")
    /**
     * 当前调用者是否是合约归属者
     */
    function
        protocol_restrict : () => unit
        protocol_restrict() =
            require(Call.caller == state.owner, "PROTOCOL_RESTRICTED")

    /**
     * 生成预测ID
     */
    function
        generate_market_id : () => string
        generate_market_id() =
             Bytes.to_str(Crypto.sha256(String.concat(Address.to_str(Call.caller), Int.to_str(Chain.timestamp))))


    //获取最多投票的结果
    function
        most_of : (list('a)) => 'a
        most_of((x :: xs) ) =
            most_of_(x, {[x] = 1}, xs)


    function
        most_of_ : ('a, map('a, int), list('a)) => 'a
        most_of_(most, counts , xs) =
            switch(xs)
                [] => (most)
                (x :: xs) =>
                    let counts' = counts{ [x = 0] @ n = n + 1 }
                    if (counts'[x] >= counts'[most])
                        most_of_(x, counts', xs)
                    else
                        most_of_(most, counts', xs)

    entrypoint
        is_repeat : (list(int)) => bool
        is_repeat((x :: xs) ) =
            if(is_repeat_(x, {[x] = 1}, xs) == 1)false
            else true


    function
        is_repeat_ : ('a, map('a, int), list('a)) => int
        is_repeat_(most, counts , xs) =
            switch(xs)
                [] => counts[most]
                (x :: xs) =>
                    let counts' = counts{ [x = 0] @ n = n + 1 }
                    if (counts'[x] >= counts'[most])
                        is_repeat_(x, counts', xs)
                    else
                        is_repeat_(most, counts', xs)






